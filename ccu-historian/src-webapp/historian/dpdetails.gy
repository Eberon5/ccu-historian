/*
    CCU-Historian, a long term archive for the HomeMatic CCU
    Copyright (C) 2011-2018 MDZ (info@ccu-historian.de)

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/
import mdz.ccuhistorian.webapp.TrendParameters
import mdz.hc.DataPoint
import groovy.transform.Field
import static mdz.ccuhistorian.webapp.TextFormat.*
import static mdz.ccuhistorian.webapp.WebUtilities.*

@Field
private static final int NUM_STRING_ENTRIES = 50

// setup page
def renderer=new mdz.ccuhistorian.webapp.PageRenderer(servlet: this)

def bitToText(value, bitMask) {
	value==null?'':(value & bitMask?'Ja':'Nein')
}

renderer.head={
	title 'CCU-historian: Datenpunktdetails'
}

renderer.content={
	def trendParams=TrendParameters.from(request, database, webServer.config.trendDesigns)
	def apiKey=webServer.config.apiKeys ? [k:webServer.config.apiKeys[0]] : [:]
	
	trendParams.groups.values().dataPoints.flatten().each { dp ->
		def last=database.getLast(dp)
		
		div(class:'panel panel-default') {
			div(class:'panel-heading') {
				h3 class:'panel-title', dp.displayName
			}
			div(class:'panel-body') {
				// list data point meta data
				div(class:'row') {
					div(class:'col-md-6') {
						table(class:'table table-bordered') {
							tbody {
								tr { 
									th 'Schnittstelle'
									td dp.id.interfaceId 
								}	
								tr { 
									th 'Kanalname'
									td dp.attributes.displayName 
								}	
								tr { 
									th 'Seriennr.:Kanalnr.'
									td dp.id.address 
								}	
								tr { 
									th 'Parameter'
									td dp.id.identifier
								}	
								tr { 
									th 'Historian-ID'
									td dp.idx 
								}	
								tr { 
									th 'Aktueller Wert'
									td format(last?.value) 
								}	
								tr { 
									th 'Minimum'
									td formatNumber(dp.attributes.minimum) 
								}	
								tr { 
									th 'Maximum'
									td formatNumber(dp.attributes.maximum) 
								}	
								tr { 
									th 'Einheit'
									td dp.attributes.unit 
								}	
								tr { 
									th 'Zeitstempel'
									td formatDate(last?.timestamp) 
								}	
							} 
						}
					}
					div(class:'col-md-6') {
						table(class:'table table-bordered') {
							tbody {
								tr { 
									th 'Standardwert'
									td format(dp.attributes.defaultValue) 
								}
								tr { 
									th 'Datentyp'
									td dp.attributes.type 
								}
								tr { 
									th 'Lesbar'
									td bitToText(dp.attributes.operations, DataPoint.ATTR_OPERATIONS_READ) 
								}
								tr { 
									th 'Beschreibbar'
									td bitToText(dp.attributes.operations, DataPoint.ATTR_OPERATIONS_WRITE) 
								}
								tr {
									 th 'Ereignis'
									 td bitToText(dp.attributes.operations, DataPoint.ATTR_OPERATIONS_EVENT) 
								}
								tr { 
									th 'Sichtbar'
									td bitToText(dp.attributes.flags, DataPoint.ATTR_FLAGS_VISIBLE) 
								}
								tr { 
									th 'Wartungsmeldung'
									td bitToText(dp.attributes.flags, DataPoint.ATTR_FLAGS_SERVICE) 
								}
								tr { 
									th 'Man. Best채tigung'
									td bitToText(dp.attributes.flags, DataPoint.ATTR_FLAGS_STICKY) 
								}
								tr { 
									th 'Tabellenname'
									td dp.historyTableName 
								}
								tr { 
									th 'Anzahl Eintr채ge'
									def total=database.getCount(dp, null, null)
									def last24h=database.getCount(dp, new Date(System.currentTimeMillis()-24*3600*1000), null)
									td  "$total gesamt, aktuell $last24h pro Tag"
								}
							}
						}
					}
				}
				if (!dp.historyString) {
					// show trends
					h4 '48h-Trend'
					p {
						img alt:'24h-Trend', src:buildUrl('../query/trend', [dp1:dp.idx, b:'-48h'], apiKey), 
							width:TrendParameters.DEFAULT_WIDTH, height:TrendParameters.DEFAULT_HEIGHT
					}
					a class:'btn btn-default', href:buildUrl('../query/csv', [dp1:dp.idx, b:'-48h'], apiKey), role:'button', 'Export CSV'
					h4 'Monats-Trend'
					p {
						img alt:'Wochen-Trend', src:buildUrl('../query/trend', [dp1:dp.idx, b:'-1M'], apiKey),
							width:TrendParameters.DEFAULT_WIDTH, height:TrendParameters.DEFAULT_HEIGHT
					}
					a class:'btn btn-default', href:buildUrl('../query/csv', [dp1:dp.idx, b:'-1M'], apiKey), role:'button', 'Export CSV'
				} else {
					// show table
					h4 'Werte der letzten Woche'
					def end=new Date()
					def timeSeries=database.getTimeSeriesRaw(dp, end-7, end)
					def toMany=timeSeries.size()>NUM_STRING_ENTRIES
					if (toMany) {
						timeSeries=timeSeries.takeRight(NUM_STRING_ENTRIES).sort { a, b -> b.timestamp <=> a.timestamp}
					} else {
						timeSeries=timeSeries.iterator().reverse()
					}
					table(class:'table table-bordered table-hover') {
						thead {
							tr {
								th 'Zeitstempel'
								th 'Wert'
								th 'Status'
							}
						}
						tbody {
							timeSeries.each { entry ->
								tr {
									td formatDate(entry.timestamp)
									td entry.value
									td class:'text-right', entry.state
								}
							}
						}
					}
					if (toMany) {
						div(class:'alert alert-warning alert-dismissible', role:'alert') {
							button(type:'button', class:'close', 'data-dismiss':'alert') {
								span { mkp.yieldUnescaped '&times;' }
							}
							span class:'glyphicon glyphicon-exclamation-sign'
							mkp.yield " Zu viele Eintr채ge! Es werden die letzten $NUM_STRING_ENTRIES Eintr채ge angezeigt!"
						}
					}
					// TODO: CSV export
				}
			}
		}
	}	
}

// render page
renderer.render()
