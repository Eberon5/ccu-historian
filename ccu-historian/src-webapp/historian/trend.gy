import groovy.transform.Field
import java.util.logging.Logger
import mdz.ccuhistorian.webapp.TrendParameters
import mdz.ccuhistorian.webapp.TimeRange
import mdz.ccuhistorian.webapp.WebUtilities
import static mdz.ccuhistorian.webapp.WebUtilities.*

@Field
private final static int NUM_OF_GROUPS = 8
@Field
private final static int MAX_NUM_DATA_POINTS = 20

@Field
private final static timeNavigations=[
	[id:'fast-backward', icon:'glyphicon-backward', func: { b, e ->
		def d=e-b; [b-d, e-d]
	}],
	[id:'backward', icon:'glyphicon-triangle-left', func: { b, e ->
		def d=(e-b)/3; [b-d, e-d]
	}],
	[id:'zoom-in', icon:'glyphicon-zoom-in', func: { b, e ->
		def d=(e-b)/3
		// force min. time range of 10 seconds
		if (d<10000) d=(e-b-10000)/2 
		[b+d, e-d]
	}],
	[id:'refresh', icon:'glyphicon-refresh', func: null /* do nothing */],
	[id:'zoom-out', icon:'glyphicon-zoom-out', func: { b, e ->
		def d=e-b; [b-d, e+d]
	}],
	[id:'forward', icon:'glyphicon-triangle-right', func: { b, e ->
		def d=(e-b)/3; [b+d, e+d]
	}],
	[id:'fast-forward', icon:'glyphicon glyphicon-forward', func: { b, e ->
		def d=e-b; [b+d, e+d]
	}],
]

@Field
private final static presetRanges=[
	[id:'preset-last-1-hour', text:'Letzte Stunde', begin:'-1h', end:''],
	[id:'preset-last-24-hours', text:'Letzten 24 Stunden', begin:'-24h', end:''],
	[id:'preset-current-day', text:'Aktueller Tag', begin:'z', end:'1D'],
	[id:'preset-previous-day', text:'Vortag', begin:'-1D z', end:'1D'],
	[id:'preset-last-7-days', text:'Letzten 7 Tage', begin:'-7D', end:''],
	[id:'preset-current-week', text:'Aktuelle Woche', begin:'1=w z', end:'1W'],
	[id:'preset-previous-week', text:'Vorwoche', begin:'-1W 1=w z', end:'1W'],
	[id:'preset-last-31-days', text:'Letzter Monat', begin:'-1M', end:''],
	[id:'preset-current-month', text:'Aktueller Monat', begin:'1=D z', end:'1M'],
	[id:'preset-previous-month', text:'Vormonat', begin:'-1M 1=D z', end:'1M'],
	[id:'preset-last-6-months', text:'Letzten 6 Monate', begin:'-6M', end:''],
]

@Field
private final static groupHeights=[
	[height:1, text:'niedrig'],
	[height:2, text:'mittel'],
	[height:4, text:'hoch'],
]

@Field
private final static Logger log = Logger.getLogger('mdz.ccuhistorian.webapp.Trend')

// setup page
def renderer=new mdz.ccuhistorian.webapp.PageRenderer(servlet: this)

renderer.head={
	title 'CCU-historian: Trend'
}

renderer.content={
	def trendParams=new TrendParameters(request, database, webServer.config.trendDesigns)
	
	// TODO: support string histories
	trendParams.groups.values().each { group ->
		group.dataPoints.removeAll { dp -> dp.historyString } 		
	}
	
	// apply time navigation
	def navId=request.getParameter('navigate')
	def navFunc=timeNavigations.find { navId==it.id }?.func
	if (navFunc) {
		def tr=trendParams.timeRange
		def (b, e)=navFunc(tr.begin.time, tr.end.time)
		trendParams.timeRange=new TimeRange(new Date((long)b), new Date((long)e))
	}
	
	div(class:'row') {
		// inputs for time navigation	
		form(id:'time-form') {
			// forward other trend parameters
			def forwardParams=trendParams.parameters.findAll { k, vs ->
				k!='b' && k!='e'
			}
			WebUtilities.buildHiddenInputs(html, forwardParams)
	
			div(class:'col-sm-2') {
				// presets
				button(type:'button', class:'btn btn-default btn-block dropdown-toggle', 'data-toggle':'dropdown') {
					mkp.yield 'Zeitbereiche'
					span class:'caret'
				}
				ul(class:'dropdown-menu') {
					presetRanges.each { rng -> 
						li { a href:'#', id:rng.id, rng.text }
					}
				}
			}
			div(class:'col-sm-3') {
				div(class:'input-group') {
					span class:'input-group-addon', 'Start'
					input type:'text', id:'time-begin', name:'b', class:'form-control', placeholder:'Start', 
						value:trendParams.timeRange.beginText
				}
			}
			div(class:'col-sm-3') {
				div(class:'input-group') {
					span class:'input-group-addon', 'Ende'
					input type:'text', id:'time-end', name:'e', class:'form-control', placeholder:'Ende',
						value:trendParams.timeRange.endText
				}
			}
			div(class:'col-sm-2') {
				button class:'btn btn-primary btn-block', type:'submit', name:'update-timerange', value:1, 'Übernehmen'
			}
		}
		// change data points
		div(class:'col-sm-2') {
			a class:'btn btn-default btn-block', href:buildUrl('index.gy', trendParams.parameters), role:'button', 'Datenpunkte'
		}
	}

	// buttons for time navigation
	div(class:'btn-group btn-group-justified', role:'group', style:'margin-top: 0.5em') {
		timeNavigations.each { tn ->
			a(class:'btn btn-default', href:buildUrl('trend.gy', trendParams.parameters, [navigate:tn.id]), role:'button') {
				span class:'glyphicon '+tn.icon
			}
		}
	}

	// display trend graphics
	def dpCnt=trendParams.groups.values().dataPoints*.size().sum()
	if (!dpCnt /* null or 0 */) {
		// no data points alert
		div(class:'alert alert-danger', role:'alert', style:'margin-top: 0.5em') {
			span class:'glyphicon glyphicon-exclamation-sign'
			mkp.yield " Es wurden keine Datenpunkte ausgewählt!"
		} 
	} else if (dpCnt>MAX_NUM_DATA_POINTS) {
		// too many data points alert
		div(class:'alert alert-danger', role:'alert', style:'margin-top: 0.5em') {
			span class:'glyphicon glyphicon-exclamation-sign'
			mkp.yield " Es wurden zu viele Datenpunkte ausgewählt (Max. Anzahl: $MAX_NUM_DATA_POINTS)!"
		} 
	} else {
		// show trend
		p(class:'text-center', style:'margin-top: 0.5em') {
			// if required, use API key
			def apiKey=webServer.config.apiKeys ? [k:webServer.config.apiKeys[0]] : [:]
			img alt:'Trend-Darstellung', src:buildUrl('../query/trend', trendParams.parameters, apiKey),
				width:trendParams.width, height:trendParams.height
		}
		
		// trend options
		button class:"btn btn-default", type:'button', 'data-toggle':'collapse', 'data-target':'#options', 
			style:'margin-bottom: 0.5em', 'Optionen'
		form {
			// forward other trend parameters
			def forwardParams=trendParams.parameters.findAll { k, vs ->
				!k.startsWith('g') && !k.startsWith('gh') && k!='w' && k!='h'
			}
			WebUtilities.buildHiddenInputs(html, forwardParams)
	
			div(id:'options', class:'panel panel-default collapse') {
				div(class:'panel-body') {
					div(class:'row') {
						// data point groups
						div(class:'col-lg-6') {
							table(class:'table table-bordered table-hover') {
								thead {
									tr { th 'Kanal'; th 'Parameter'; th 'Gruppe' }
								}
								tbody {
									int dpIdx=1
									trendParams.groups.each { grpIdx, grp ->
										// remove unsupported group indices
										if (grpIdx>NUM_OF_GROUPS) {
											grpIdx=NUM_OF_GROUPS
										}
										grp.dataPoints.each { dp ->
											tr {
												td dp.attributes.displayName?:dp.id.address
												td dp.id.identifier
												td {
													select(class:'form-control', name:'g'+dpIdx) {
														(1..NUM_OF_GROUPS).each { optIdx ->
															def selected=(grpIdx==optIdx)?[selected:'selected']:[:]
															option(value:optIdx, *:selected, optIdx)
														}
													}
												}
											}
											dpIdx++
										}
									}
								}
							}
						}
						// group heights
						div(class:'col-lg-3') {
							table(class:'table table-bordered table-hover') {
								thead {
									tr { th 'Gruppe'; th 'Höhe' }
								}
								tbody {
									(1..NUM_OF_GROUPS).each { grpIdx ->
										def height=trendParams.groups[grpIdx]?.height?:1
										// normalize height
										height=groupHeights*.height.reverse().find { it<=height }
										tr {
											td 'Gruppe '+grpIdx
											td {
												select(class:'form-control', name:'gh'+grpIdx) {
													groupHeights.each { grpHeight ->
														def selected=(height==grpHeight.height)?[selected:'selected']:[:]
														option(value:grpHeight.height, *:selected, grpHeight.text)
													}
												}
											}
										}
									}
								}
							}
						}
						div(class:'col-lg-3') {
							// trend size
							table(class:'table table-bordered table-hover') {
								thead {
									tr { th 'Größe'; th 'Wert' }
								}
								tbody {
									tr {
										td 'Breite'
										td { input class:'form-control', name:'w', value:trendParams.width }
									}
									tr {
										td 'Höhe'
										td { input class:'form-control', name:'h', value:trendParams.height }
									}
								}
							}
							// submit button
							button class:'btn btn-primary btn-block', type:'submit', name:'update-options', value:1, 'Übernehmen'
						}
					}
				}
			}
		}
	}
}

renderer.end={
    script {
		// bind preset ranges
		presetRanges.each { rng ->
			mkp.yieldUnescaped($/
				$('#${rng.id}').on('click', function(e) {
					$('#time-begin').val('${rng.begin}');
					$('#time-end').val('${rng.end}');
					$('#time-form').submit();
				});	
			/$)
		} 
	}
}

// render page
renderer.render()
