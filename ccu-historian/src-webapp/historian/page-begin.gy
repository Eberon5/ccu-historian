import mdz.hc.itf.hm.HmSysVarInterface
import groovy.transform.Field
import org.slf4j.LoggerFactory

@Field
private final static log = LoggerFactory.getLogger('mdz.ccuhistorian.webapp.page-begin')

utils.catchToLog(log) {
	
	def ctx=session.getAttribute('ctx')
	
	// HTML document start
	print '<!doctype html>\n<html lang="de">\n<head>'
	
	// html head
	html.with {
		// standard headers
		meta charset:'utf-8'
		meta name:'viewport', content:'width=device-width, initial-scale=1'
		
		// jQuery
		link rel:'stylesheet', href:'/external/jquery-ui/jquery-ui.css' 
		script src:'/external/jquery-ui/jquery.js'
		script src:'/external/jquery-ui/jquery-ui.js'

		// custom JS and CSS		
		// TODO: new icon with size 196 x 196 pixels
		link rel:'icon', href:'/historian/ccu-historian.ico'
		link rel:'stylesheet', href:'/historian/historian_deprecated.css'
		link rel:'stylesheet', href:'/historian/historian.css'
		
		// additional head entries
		def appender=request.getAttribute('page-begin-head-appender')
		if (appender) appender(html)
	}
	
	// html body
	print '\n</head><body>'
	
	html.div(id:'menu-section') {
		// menu title
		h1 class:'ui-widget-header ui-corner-top', 'CCU-Historian'
		
		// navigation menu
		if (ctx.user.loggedIn) {
			ul(id:'main-menu') {
				// basic entries
				li { div { a href:'/historian/index.gy', 'Ãœbersicht Datenpunkte' } }
				li { div { a href:'/historian/dbmanage.html', 'Verwaltung Datenpunkte' } }
				li { div { a href:'/historian/tools.html', 'Werkzeuge' } }
				li { div { a href:'/historian/config.html', 'Konfiguration' } }
				li { div { a href:'/historian/diagnosis.html', 'Diagnose' } }
				
			   // database web access
				def port; try { port=database.webPort } catch (e) {}
				if (port)
					li { div { a href:"http://$webServer.historianAddress:$port", target:'_blank', 'Datenbank' } }

				// CCU web UI
				def ccuNo=1
				interfaceManager.interfaces.each { itfName, itf ->
					// each CCU has one HmSysVarInterface
					if (itf instanceof HmSysVarInterface) {
						li { div { a href:"http://$itf.scriptClient.address", target:'_blank', 'CCU '+(ccuNo>1?ccuNo:'') } }
						ccuNo++
					}
				}

				// configured menu entries
				webServer.config.menuLinks.each { k, v ->
					li { div { a href:v.address, v.text } }
				}
			}
			html.script { mkp.yieldUnescaped '$(function() { $("#main-menu").menu(); });' }
			
			// logout button
			if (ctx.prefs.get('password_admin', '')!='') {
				form(method:'post') {
					h2 class:'ui-widget-header', 'Benutzer'
					div(class:'ui-widget-content') {
						button style:'margin: 0.25em;', id:'logout-button', name:'logout', value:1, 'Abmelden'
					}
				}
				script { mkp. yieldUnescaped '$("#logout-button").button()' }
			}
		} else {
			div(class:'ui-widget-content') {
				p style:'padding: 0.5em;', 'Bitte melden Sie sich an!'
			}
		}
		
		// footer of menu
		h3 class:'ui-widget-header ui-corner-bottom', "Version: $utils.historianVersion"
	}
	  
	// start of content
	print '\n<div id="content-section">'
} // catchToLog
