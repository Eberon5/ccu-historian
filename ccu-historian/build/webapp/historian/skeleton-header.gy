import java.util.prefs.Preferences
import groovy.transform.Field
import java.util.logging.Logger

@Field
private final static log = Logger.getLogger('mdz.ccuhistorian.webapp.SkeletonHeader')

utils.catchToLog(log) {
	if (!session) {
		session=request.session
		session.maxInactiveInterval=1800
	}
	def ctx=session.getAttribute('ctx')
	if (!ctx) {
		ctx=[
			prefs: Preferences.userRoot().node('mdz/ccuhistorian/webpages'), 
			user: [loggedIn:false, logInFailed:false]
		]
		session.setAttribute('ctx', ctx)
	}
	if (!ctx.user.loggedIn) {
		def password_admin=ctx.prefs.get('password_admin', '')
		if (password_admin=='')
			ctx.user.loggedIn=true
		else if (params.login) {
			if (password_admin==utils.secureHash(params.login_password)) {
				ctx.user.loggedIn=true
				ctx.user.logInFailed=false
			} else {
				ctx.user.loggedIn=false
				ctx.user.logInFailed=true
			}	
		}	
	} else {
		if (params.logout) {
			ctx.user.loggedIn=false
			ctx.user.logInFailed=false
		}
	}
	
	// HTML document start
	println '<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN" "http://www.w3.org/TR/html4/loose.dtd">'
	println '<html><head>'
	
	// standard headers
	html.meta 'http-equiv':'content-type', content:'text/html; charset=utf-8'
	html.meta 'http-equiv':'expires', content:'0'
	html.meta name:'robots', content:'noindex, nofollow'
	html.link rel:'shortcut icon', type:'image/x-icon', href:'/historian/ccu-historian.ico'
	html.link rel:'stylesheet', type:'text/css', href:'/historian/historian.css'
	
} // catchToLog
