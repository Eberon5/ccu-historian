import mdz.hc.DataPoint
import mdz.ccuhistorian.Database
import groovy.transform.Field
import org.slf4j.LoggerFactory

@Field
private final static log = LoggerFactory.getLogger('mdz.ccuhistorian.webapp.index')

// setup page
request.setAttribute('pageLog', log)

request.setAttribute('pageHead', {
	title 'CCU-historian: Datenpunktliste'
	// bootstrap-table CSS
	link href:'../external/bootstrap-table/bootstrap-table.css', rel:'stylesheet'
})

request.setAttribute('pageContent', { html ->
	div(class:'panel panel-default') {
		div(class:'panel-heading') {
			h3 class:'panel-title', 'Datenpunktliste'
		}
		div(class:'panel-body') {
			form(action:'analyze.html', method:'get') {
				div(id:'toolbar', class:'btn-group') {
					button class:'btn btn-default', type:'submit', name:'trend_set', value:1, 'Trend-Darstellung'
				}
				table(
					'data-toggle':'table',
					'data-toolbar':'#toolbar', 
					'data-id-field':'id',
					'data-select-item-name':'i',
					'data-click-to-select':'true',
					'data-maintain-selected':'true',
					'data-show-columns':'true',
					'data-search':'true',
					'data-trim-on-search':'false'
				) {
					thead {
						tr {
							th 'data-checkbox':'true'
							th 'data-sortable':'true', 'Schnittstelle'
							th 'data-sortable':'true', 'Kanal'
							th 'data-sortable':'true', 'Parameter'
							th 'data-sortable':'true', 'Raum'
							th 'data-sortable':'true', 'Gewerk'
							th 'data-field':'id', 
							   'data-visible':'false', 
							   'data-sortable':'true', 
							   'data-align':'right', 
							   'ID'
							th 'data-visible':'false', 'data-align':'right', 'Akt. Wert'
							th 'data-visible':'false', 'data-sortable':'true', 'Einheit'
							th 'data-visible':'false', 'Zeitstempel'
							th 'data-visible':'false', 'Details'
						}
					}
					tbody {
						database.dataPoints.findAll { !it.historyHidden }.each { dp ->
							def last=database.getLast(dp)
							html.tr {
								// TODO:
								//def disabled=dp.historyString?[disabled:'disabled']:[:]
								td '' //{ input(type:'checkbox', name:'i', value:dp.idx, *:disabled) }
								td dp.id.interfaceId
								td dp.attributes.displayName?:dp.id.address
								td dp.id.identifier
								td dp.attributes.room
								td dp.attributes.function
								td class:'number', dp.idx
								td class:'number', utils.format(last?.value)
								td dp.attributes.unit
								td utils.format(last?.timestamp)
								td { a href:"dpdetails.html?i=$dp.idx", 'Details' } 
							}
						}
					}
				}
			}
		}
	}
})

request.setAttribute('pageEnd', {
	// bootstrap-table JS
	script src:'../external/bootstrap-table/bootstrap-table.js'
	script src:'../external/bootstrap-table/bootstrap-table-de-DE.js'
})

// render page
include 'page-template.gy'
